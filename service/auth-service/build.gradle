plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.9'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.sonarqube' version '5.1.0.4882'
	id 'jacoco'
}

group = 'FromProm'
version = '0.0.1-SNAPSHOT'
description = 'auth-service'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// 1. AWS SDK for Java v2 (Cognito & DynamoDB)
	implementation 'software.amazon.awssdk:cognitoidentityprovider:2.20.0'
	implementation 'software.amazon.awssdk:dynamodb-enhanced:2.20.0'
	implementation 'software.amazon.awssdk:dynamodb:2.20.0'
	
	// 2. AWS STS (IRSA - Web Identity Token 지원)
	implementation 'software.amazon.awssdk:sts:2.20.0'

	// 3. Lombok (Getter, Setter 자동 생성)
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// 4. Spring Boot Starter (기본 설정)
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework:spring-tx'

	// 5. AWS SDK for SNS
	implementation 'software.amazon.awssdk:sns:2.20.0'

	// 6. OpenTelemetry API (trace context 전파용)
	implementation 'io.opentelemetry:opentelemetry-api:1.32.0'

	// 7. AWS X-Ray SDK for manual instrumentation (가장 확실한 트레이싱)
	implementation 'com.amazonaws:aws-xray-recorder-sdk-core:2.15.0'
	implementation 'com.amazonaws:aws-xray-recorder-sdk-aws-sdk-v2:2.15.0'
	implementation 'com.amazonaws:aws-xray-recorder-sdk-aws-sdk-v2-instrumentor:2.15.0'

}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
	}
}

sonarqube {
	properties {
		property "sonar.projectKey", "fromprom-auth"
		property "sonar.projectName", "FromProm Auth Service"
		property "sonar.sources", "src/main/java"
		property "sonar.tests", "src/test/java"
		property "sonar.java.binaries", "build/classes/java/main"
		property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
	}
}
